! Problem 1;
! 假定所有发电机工作时功率相同;
MODEL:
SETS:
TIMES:CAPACITY,HOURS;
GENERATERS:POWERMIN,POWERMAX,BASECOST,OVERCOST,STARTCOST,MAXNUMB;
LINKS(TIMES,GENERATERS):NUMBERS,POWER,deltan;
ENDSETS
DATA:
TIMES = T1 T2 T3 T4 T5 T6 T7;
GENERATERS = G1 G2 G3 G4;

HOURS    = 6     3     3     2     4     4     2    ;
CAPACITY = 12000 32000 25000 36000 25000 30000 18000;
POWERMIN = 750   1000  1200  1800;
POWERMAX = 1750  1500  2000  3500;
BASECOST = 2250  1800  3750  4800;
OVERCOST = 2.70  2.20  1.80  3.80;
STARTCOST= 5000  1600  2400  1200;
MAXNUMB  = 10    4     8     3   ;

ENDDATA

! The objective;
C1 = @SUM( LINKS(I,J): NUMBERS(I,J) * BASECOST(J) * HOURS(I));!计算固定成本;
C2 = @SUM( LINKS(I,J): (POWER(I,J)-POWERMIN(J)) * HOURS(I)
                       * NUMBERS(I,J) * OVERCOST(J));!计算功率成本;
C3 = @SUM( LINKS(I,J)|I#ge#2: @if(deltan(I,J) #gt# 0,deltan(I,J) * STARTCOST(J),0));
!计算2-7时段的启动成本;
C4 = @SUM( GENERATERS(J): NUMBERS(1,J) * STARTCOST(J));!计算1时段的启动成本;
min = C1+C2+C3+C4;!objective function;
! The constraints;
@for( LINKS(I,J)|I#ge#2: deltan(I,J) = NUMBERS(I,J) - NUMBERS(I-1,J));!interger constraints;
@for( LINKS(I,J): @bnd(0,NUMBERS(I,J),MAXNUMB(J)));!数量约束范围;
@for( LINKS: @gin(NUMBERS));!interger constraints;
@for( LINKS(I,J): @bnd(POWERMIN(J),POWER(I,J),POWERMAX(J)));!约束功率范围;
@for( TIMES(I): @sum(LINKS(I,J): NUMBERS(I,J) * POWER(I,J)) >= CAPACITY(I));!总功率范围;

